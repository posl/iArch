grammar jp.ac.kyushu.iarch.archdsl.ArchDSL with org.eclipse.xtext.common.Terminals

generate archDSL "http://www.ac.jp/kyushu/iarch/archdsl/ArchDSL"

Model:
	(interfaces+=Interface |
	u_interfaces+=UncertainInterface |
	behaviors+=Behavior |
	u_connectors+=UncertainConnector |
	connectors+=Connector|
	dflows+=Dflow)*;

UncertainInterface:
	"uncertain component" name=ID ("extends" superInterface=[Interface])? "{"
	(altmethods+=AltMethod |
	optmethods+=OptMethod)*
	"}";

Interface:
	"interface component" name=ID "{"
	methods+=Method*
	"}";

Method:
	type=ID name=ID "(" (param+=Param ("," param+=Param)*)? ");";

AltMethod:
	"{"
	methods+=Method ("," methods+=Method)*
	"}";

OptMethod:
	"["
	method=Method
	"]";

Param:
	type=ID name=ID;

cParam :
    name=ID
;

dParam :
    name=ID
;

Dflow:
    "interface dflow" name=ID "("(dparam+=dParam(","dparam+=dParam)*)?")" "{"
        dataflows+=Dataflow*
     "}";

Dataflow:
        dparam=[dParam] "="
        ("|"getflow+=[dParam]"|""->>"
        call+=[Method|FQN]
        "->>""|"getflow+=[dParam]"|""->>"
        getIf+=[Interface]
        ("->>""|"getflow+=[dParam]"|""->>"
        call+=[Method|FQN])*
        "->>"end=[dParam])?";"
;

/* some definition Directions
*
* "getflow"-> get flow_name (for instance:"state"),
* "call"-> get Method_name (for instance:"getState" and so on),
* "getIf"-> get Interface_name (for instance:"Subject" or "Observer").
*/

Connector:
	"interface connector" name=ID "{"
	behaviors+=Behavior*
	"}";

UncertainConnector:
	"uncertain connector" name=ID ("extends" superInterface=[Connector])? "{"
	u_behaviors+=UncertainBehavior*
	"}";

Behavior:
	interface=[Interface] "=" "(" (call+=[Method|FQN] ("->" call+=[Method|FQN])* "->" end=[Interface])? ");";

SuperMethod:
	Method | OptMethod | AltMethod;

UncertainBehavior:
	"u" name=ID "=" "("
	(call+=SuperCall ("->" call+=SuperCall)* "->" end=[Interface])?
	");";

CertainCall:
	name=[SuperMethod|FQN];

OptCall:
	"[" name=[SuperMethod|FQN] "]";

AltCall:
	"{" (opt?="null" ",")? name=[SuperMethod|FQN] ("," (a_name+=[SuperMethod|FQN] | (opt?="null")?))* "}";

SuperCall:
	CertainCall | OptCall | AltCall;

FQN:
	ID ("." ID)*;