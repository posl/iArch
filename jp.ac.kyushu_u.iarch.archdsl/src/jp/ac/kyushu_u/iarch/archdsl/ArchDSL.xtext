grammar jp.ac.kyushu_u.iarch.archdsl.ArchDSL with org.eclipse.xtext.common.Terminals

generate archDSL "http://www.kyushu-u.ac.jp/iarch/archdsl/ArchDSL"

Model:
	(interfaces+=Interface |
	u_interfaces+=UncertainInterface |
	behaviors+=Behavior |
	u_connectors+=UncertainConnector |
	connectors+=Connector)*;

UncertainInterface:
	"uncertain component" name=ID ("extends" superInterface=[Interface])? "{"
	(altmethods+=AltMethod |
	optmethods+=OptMethod)*
	"}";

Interface:
	"interface component" name=ID "{"
	methods+=Method*
	"}";

Method:
	(annotations+=Annotation)* type=ID name=ID "(" (param+=Param ("," param+=Param)*)? ");";

AltMethod:
	(annotations+=Annotation)* "{"
	methods+=Method ("," methods+=Method)*
	"}";

OptMethod:
	(annotations+=Annotation)* "["
	method=Method
	"]";

Param:
	type=ID name=ID;

Annotation:
	"@" name=ID ("(" args+=Expression ("," args+=Expression)* ")")?;

Expression:
	STRING | INT | DOUBLE | BOOLEAN;

terminal DOUBLE:
	INT "." INT;

terminal BOOLEAN:
	"true" | "false";


Connector:
	"interface connector" name=ID "{"
	behaviors+=Behavior*
	"}";

UncertainConnector:
	"uncertain connector" name=ID ("extends" superInterface=[Connector])? "{"
	u_behaviors+=UncertainBehavior*
	"}";

Behavior:
	interface=[Interface] "=" "(" (call+=[Method|FQN] ("->" call+=[Method|FQN])* "->" end=[Interface])? ");";

SuperMethod:
	Method | OptMethod | AltMethod;

UncertainBehavior:
	// now preceding "u" is optional. 
	"u"? name=ID "=" "("
	(call+=SuperCall ("->" call+=SuperCall)* "->" end=[Interface])?
	");";

CertainCall:
	(annotations+=Annotation)* name=[SuperMethod|FQN];

OptCall:
	(annotations+=Annotation)* "[" name=[SuperMethod|FQN] "]";

AltCallChoice:
	(annotations+=Annotation)* name=[SuperMethod|FQN];

AltCall:
	(annotations+=Annotation)* "{" (opt?="null" ",")? name=AltCallChoice ("," (a_name+=AltCallChoice | (opt?="null")?))* "}";

SuperCall:
	CertainCall | OptCall | AltCall;

FQN:
	ID ("." ID)*;